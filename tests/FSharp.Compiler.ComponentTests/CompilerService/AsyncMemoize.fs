module CompilerService.AsyncMemoize

open System
open System.Threading
open Internal.Utilities.Collections
open System.Threading.Tasks
open System.Diagnostics

open FSharp.Compiler.DiagnosticsLogger
open FSharp.Compiler.Diagnostics

open Xunit

type Ordered(tag) =
    let mutable current = 1

    let reached n = fun () -> n <= current

    let waitAsync n = async {
        while reached n () |> not do
            do! Async.Sleep 1
    }
        
    let wait n = SpinWait.SpinUntil(reached n)

    let step n msg = 
        printfn $"{tag}: step {n} {msg}"
        Interlocked.CompareExchange(&current, n + 1, n) |> ignore

    member _.Step(n, msg) = wait n; step n msg

    member _.StepAsync(n, msg) = async {
        do! waitAsync n
        step n msg
    }

let step(ordered: Ordered) n msg = ordered.Step(n, msg)
let stepAsync (ordered: Ordered) n msg = ordered.StepAsync(n, msg)
let steps tag = (Ordered tag) |> step

let startToCancel computation =
    let cts = new CancellationTokenSource()
    Async.Start(computation |> Async.Catch |> Async.Ignore, cancellationToken = cts.Token)
    let cancel() = cts.Cancel()
    cancel

let internal wrapKey key =
    { new ICacheKey<_, _> with
        member _.GetKey() = key
        member _.GetVersion() = Unchecked.defaultof<_>
        member _.GetLabel() = match key.ToString() with | null -> "" | s -> s
    }

[<AutoOpen>]
module internal JobEvents =
    let tap f x = f x; x
    let tapPrint = Event.map (tap (printfn "%A"))

    let mapEvent (cache: AsyncMemoize<_, _, _>) =
        cache.Event |> Event.map (fun (jobEvent, (_,k,_)) -> jobEvent, k) |> tapPrint

    let collectEvents cache =
        cache |> mapEvent |> Event.scan (fun es e -> e :: es) [] |> Event.map List.rev

    /// Exposes a live view of the list of JobEvents generated by AsyncMemoize.
    let observe cache =
        let mutable recorded = []
        let update next = recorded <- next
        collectEvents cache |> Event.add update
        fun () -> recorded

    let countOf value count events = events |> Seq.filter (fst >> (=) value) |> Seq.length |> (=) count

    let received value events = events |> Seq.exists (fst >> (=) value)

    let waitUntil events condition =
        task {
            let s = System.Diagnostics.Stopwatch.StartNew()
            while events() |> condition |> not do
                do! Task.Delay 100
                if s.ElapsedMilliseconds > 5000 then failwith "timeout"
        }

[<Fact>]
let ``Basics``() =
    task {
        let computation key = async {
            do! Async.Sleep 1
            return key * 2
        }

        let memoize = AsyncMemoize<int, int, int>()
        let events = observe memoize

        let result =
            seq {
                memoize.Get(wrapKey 5, computation 5)
                memoize.Get(wrapKey 5, computation 5)
                memoize.Get(wrapKey 2, computation 2)
                memoize.Get(wrapKey 5, computation 5)
                memoize.Get(wrapKey 3, computation 3)
                memoize.Get(wrapKey 2, computation 2)
            }
            |> Async.Parallel
            |> Async.RunSynchronously

        let expected = [| 10; 10; 4; 10; 6; 4|]

        Assert.Equal<int array>(expected, result)

        do! waitUntil events (countOf Finished 3)
        let groups = events() |> Seq.groupBy snd |> Seq.toList
        Assert.Equal(3, groups.Length)
        for key, events in groups do
            Assert.Equal<Set<(JobEvent * int)>>(Set [ Requested, key; Started, key; Finished, key ], Set events)
    }

[<Fact>]
let ``We can cancel a job`` () =
    task {

        let computation = async {
            do! Async.Sleep 10_000
            // failwith "Should be canceled before it gets here"
        }

        let memoize = AsyncMemoize<_, int, _>()
        let events = observe memoize

        let key = 1

        let cancel = memoize.Get(wrapKey 1, computation) |> startToCancel

        do! waitUntil events (received Started)

        cancel()

        do! waitUntil events (received Canceled)

        Assert.Equal<_ list>(
            [ 
                Requested, key
                Started, key
                Canceled, key
            ], 
            events()
        )
    }

[<Fact>]
let ``Job is completed if only one requestor cancels`` () =
    task {
        let step = steps ""

        let computation key = async {
            step 1 "job started"
            step 4 ""
            return key * 2
        }

        let memoize = AsyncMemoize<_, int, _>()
        let events = observe memoize

        let key = 1

        let cancel1 = memoize.Get(wrapKey key, computation key) |> startToCancel

        step 2 "Get"

        let t2 = memoize.Get(wrapKey key, computation key) |> Async.StartAsTask
        let t3 = memoize.Get(wrapKey key, computation key) |> Async.StartAsTask

        do! waitUntil events (countOf Requested 3)

        cancel1()

        step 3 "job 1 canceled"

        let! result = t2
        Assert.Equal(2, result)
        let! result = t3
        Assert.Equal(2, result)

        Assert.Equal<_ list>(
          [ Requested, key
            Started, key
            Requested, key
            Requested, key
            Finished, key ],
            events()
        )
    }

[<Fact>]
let ``Job is restarted for another request after the first request canceled`` () =
    task {
        let ordered = Ordered ""
        let step = step ordered

        let computation key = async {
            step 2 "job started"
            do! stepAsync ordered 5 "after first cancellation"
            return key * 2
        }
        
        let memoize = AsyncMemoize<_, int, _>()
        let events = observe memoize

        let key = 1

        let cancel1 =  memoize.Get(wrapKey key, computation key) |> startToCancel
        do! waitUntil events (received Requested)
        step 1 "first Get"
        step 3 "after job started"
        cancel1()
        do! waitUntil events (received Canceled)
        step 4 "received Canceled"

        let task2 = memoize.Get(wrapKey key, computation key) |> Async.StartAsTask

        let! result = task2

        Assert.Equal(2, result)

        do! waitUntil events (received Finished)

        Assert.Equal<_ list>(
          [ Requested, key
            Started, key
            Canceled, key
            Requested, key
            Started, key
            Finished, key ],
            events()
        )
    }


type ExpectedException() =
    inherit Exception()

[<Fact(Skip="Flaky")>]
let ``Stress test`` () =

    let seed = System.Random().Next()

    let rng = System.Random seed
    let threads = 30
    let iterations = 30
    let maxDuration = 100
    let minTimeout = 0
    let maxTimeout = 500
    let exceptionProbability = 0.01
    let gcProbability = 0.1
    let stepMs = 10
    let keyCount = rng.Next(5, 200)
    let keys = [| 1 .. keyCount |]

    let testTimeoutMs = threads * iterations * maxDuration * 2

    let intenseComputation durationMs result =
        async {
            if rng.NextDouble() < exceptionProbability then
                raise (ExpectedException())
            let s = Stopwatch.StartNew()
            let mutable number = 0
            while (int s.ElapsedMilliseconds) < durationMs do
                number <- number + 1 % 12345
            return [result]
        }

    let rec sleepyComputation durationMs result =
        async {
            if rng.NextDouble() < (exceptionProbability / (float durationMs / float stepMs)) then
                raise (ExpectedException())
            if durationMs > 0 then
                do! Async.Sleep (min stepMs durationMs)
                return! sleepyComputation (durationMs - stepMs) result
            else
                return [result]
        }

    let rec mixedComputation durationMs result =
        async {
            if durationMs > 0 then
                if rng.NextDouble() < 0.5 then
                    let! _ = intenseComputation (min stepMs durationMs) ()
                    ()
                else
                    let! _ = sleepyComputation (min stepMs durationMs) ()
                    ()
                return! mixedComputation (durationMs - stepMs) result
            else
                return [result]
        }

    let computations = [|
        intenseComputation
        sleepyComputation
        mixedComputation
    |]

    let cache = AsyncMemoize<int, int, int list>(keepStrongly=5, keepWeakly=10)

    let mutable started = 0
    let mutable canceled = 0
    let mutable timeout = 0
    let mutable failed = 0
    let mutable completed = 0

    let test =
        seq {
            for _ in 1..threads do
                let rec loop iteration =
                    task {
                        if gcProbability > rng.NextDouble() then
                            GC.Collect(2, GCCollectionMode.Forced, false)

                        let computation = computations[rng.Next computations.Length]
                        let durationMs = rng.Next maxDuration
                        let timeoutMs = rng.Next(minTimeout, maxTimeout)
                        let key = keys[rng.Next keys.Length]
                        let result = key * 2
                        let job = cache.Get(wrapKey key, computation durationMs result)
                        let cts = new CancellationTokenSource()
                        let runningJob = Async.StartAsTask(job, cancellationToken = cts.Token)
                        cts.CancelAfter timeoutMs
                        Interlocked.Increment &started |> ignore
                        try
                            let! actual = runningJob
                            Assert.Equal(result, actual.Head)
                            Interlocked.Increment &completed |> ignore
                        with
                            | :? TaskCanceledException as _e ->
                                Interlocked.Increment &canceled |> ignore
                            | :? OperationCanceledException as _e ->
                                Interlocked.Increment &canceled |> ignore
                            | :? TimeoutException -> Interlocked.Increment &timeout |> ignore
                            | :? ExpectedException -> Interlocked.Increment &failed |> ignore
                            | :? AggregateException as ex when
                                ex.Flatten().InnerExceptions |> Seq.exists (fun e -> e :? ExpectedException) ->
                                Interlocked.Increment &failed |> ignore
                            | e ->
                                failwith $"Seed {seed} failed on iteration {iteration}: %A{e}"
                        if iteration < iterations then
                            return! loop (iteration + 1)
                        return ()
                    }
                loop 1
        }
        |> Task.WhenAll

    if not (test.Wait testTimeoutMs) then failwith "Test timed out - most likely deadlocked"
    
    Assert.Equal (threads * iterations, started)
    // Assert.Equal<int * int * int * int * int>((0,0,0,0,0),(started, completed, canceled, failed, timeout))
    Assert.Equal (started, completed + canceled + failed + timeout)

    Assert.True ((float completed) > ((float started) * 0.1), "Less than 10 % completed jobs")


[<Theory>]
// [<InlineData(true, 1)>]
[<InlineData(false, 2)>]
let ``Cancel running jobs with the same key`` cancelDuplicate expectFinished =
    let cache = AsyncMemoize(cancelDuplicateRunningJobs=cancelDuplicate)

    let _ = observe cache

    let mutable started = 0
    let mutable finished = 0

    let job1started = new ManualResetEventSlim(false)
    let job1finished = new ManualResetEventSlim(false)

    let jobCanContinue = new ManualResetEventSlim(false)

    let job2started = new ManualResetEventSlim(false)
    let job2finished = new ManualResetEventSlim(false)

    let work onStart onFinish = async {
        Interlocked.Increment &started |> ignore
        onStart() |> ignore
        jobCanContinue.Wait()
        do! Async.Sleep 100
        Interlocked.Increment &finished |> ignore
        onFinish() |> ignore
    }

    let key1 =
        { new ICacheKey<_, _> with
                member _.GetKey() = 1
                member _.GetVersion() = 1
                member _.GetLabel() = "key1" }

    cache.Get(key1, work job1started.Set job1finished.Set) |> Async.Catch |> Async.Ignore |> Async.Start

    job1started.Wait()

    let key2 =
        { new ICacheKey<_, _> with
                member _.GetKey() = key1.GetKey()
                member _.GetVersion() = key1.GetVersion() + 1
                member _.GetLabel() = "key2" }

    cache.Get(key2, work job2started.Set job2finished.Set ) |> Async.Catch |> Async.Ignore |> Async.Start

    job2started.Wait()

    jobCanContinue.Set() |> ignore
        
    job2finished.Wait()
        
    if not cancelDuplicate then
        job1finished.Wait()

    Assert.Equal((2, expectFinished), (started, finished))

type DummyException(msg) =
    inherit Exception(msg)

[<Fact>]
let ``Preserve thread static diagnostics`` () = 

    let seed = System.Random().Next()

    let rng = System.Random seed
    
    let job1Cache = AsyncMemoize()
    let job2Cache = AsyncMemoize()

    let job1 (input: string) = async {
        let! _ = Async.Sleep (rng.Next(1, 30))
        let ex = DummyException("job1 error")
        DiagnosticsThreadStatics.DiagnosticsLogger.ErrorR(ex)
        return Ok input
    }

    let job2 (input: int) = async {
        
        DiagnosticsThreadStatics.DiagnosticsLogger.Warning(DummyException("job2 error 1"))

        let! _ = Async.Sleep (rng.Next(1, 30))

        let key = { new ICacheKey<_, _> with
                        member _.GetKey() = "job1"
                        member _.GetVersion() = input
                        member _.GetLabel() = "job1" }

        let! result = job1Cache.Get(key, job1 "${input}" )

        DiagnosticsThreadStatics.DiagnosticsLogger.Warning(DummyException("job2 error 2"))

        return input, result

    }

    let tasks = seq {
        for i in 1 .. 100 do

            task {
                let diagnosticsLogger =
                    CompilationDiagnosticLogger($"Testing task {i}", FSharpDiagnosticOptions.Default)

                use _ = new CompilationGlobalsScope(diagnosticsLogger, BuildPhase.Optimize)

                DiagnosticsThreadStatics.DiagnosticsLogger.Warning(DummyException("task error"))


                let key = { new ICacheKey<_, _> with
                                member _.GetKey() = "job2"
                                member _.GetVersion() = rng.Next(1, 10)
                                member _.GetLabel() = "job2" }

                let! result = job2Cache.Get(key, job2 (i % 10))

                let diagnostics = diagnosticsLogger.GetDiagnostics()

                Assert.Equal(4, diagnostics.Length)

                return result, diagnostics
            }
    }

    let results = (Task.WhenAll tasks).Result

    let diagnosticCounts = results |> Seq.map snd |> Seq.map Array.length |> Seq.groupBy id |> Seq.map (fun (k, v) -> k, v |> Seq.length) |> Seq.sortBy fst |> Seq.toList

    Assert.Equal<(int * int) list>([4, 100], diagnosticCounts)

    let diagnosticMessages = results |> Seq.map snd |> Seq.map (Array.map (fun (d, _) -> d.Exception.Message) >> Array.toList) |> Set

    Assert.Equal<Set<_>>(Set [["task error"; "job2 error 1"; "job1 error"; "job2 error 2"; ]], diagnosticMessages)


[<Fact>]
let ``Preserve thread static diagnostics already completed job`` () =

    let cache = AsyncMemoize()

    let _ = observe cache

    let key = { new ICacheKey<_, _> with
                    member _.GetKey() = 1
                    member _.GetVersion() = 1
                    member _.GetLabel() = "job1" }

    let job (input: string) = async {
        let ex = DummyException($"job {input} error")
        DiagnosticsThreadStatics.DiagnosticsLogger.ErrorR(ex)
        return Ok input
    }

    task {

        let diagnosticsLogger = CompilationDiagnosticLogger($"Testing", FSharpDiagnosticOptions.Default)

        use _ = new CompilationGlobalsScope(diagnosticsLogger, BuildPhase.Optimize)

        let! _ = cache.Get(key, job "1" )
        let! _ = cache.Get(key, job "2" )

        let diagnosticMessages = diagnosticsLogger.GetDiagnostics() |> Array.map (fun (d, _) -> d.Exception.Message) |> Array.toList

        Assert.Equal<_ list>(["job 1 error"; "job 1 error"], diagnosticMessages)

    }


[<Fact>]
let ``We get diagnostics from the job that failed`` () =

    let cache = AsyncMemoize()

    let key = { new ICacheKey<_, _> with
                    member _.GetKey() = "job1"
                    member _.GetVersion() = 1
                    member _.GetLabel() = "job1" }

    let job = async {
        let ex = DummyException($"job error")

        // no recovery
        DiagnosticsThreadStatics.DiagnosticsLogger.Error ex
        return 5
    }

    task {
        let logger = CapturingDiagnosticsLogger("AsyncMemoize diagnostics test")

        SetThreadDiagnosticsLoggerNoUnwind logger

        do! cache.Get(key, job ) |> Async.Catch |> Async.Ignore

        let messages = logger.Diagnostics |> List.map fst |> List.map _.Exception.Message

        Assert.Equal<_ list>(["job error"], messages)
    }
